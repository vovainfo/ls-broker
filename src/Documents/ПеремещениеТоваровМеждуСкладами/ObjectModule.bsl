#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПеремещениеТоваровМеждуСкладамиТовары.Товар КАК Товар,
		|	ПеремещениеТоваровМеждуСкладамиТовары.ГрузовоеМесто КАК ГрузовоеМесто,
		|	СУММА(ПеремещениеТоваровМеждуСкладамиТовары.Количество) КАК Количество
		|ПОМЕСТИТЬ втТовары
		|ИЗ
		|	Документ.ПеремещениеТоваровМеждуСкладами.Товары КАК ПеремещениеТоваровМеждуСкладамиТовары
		|ГДЕ
		|	ПеремещениеТоваровМеждуСкладамиТовары.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ПеремещениеТоваровМеждуСкладамиТовары.Товар,
		|	ПеремещениеТоваровМеждуСкладамиТовары.ГрузовоеМесто
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ГрузовоеМесто,
		|	Товар
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТовары.Товар,
		|	втТовары.ГрузовоеМесто,
		|	втТовары.Количество,
		|	&Период КАК Период,
		|	&Склад КАК Склад
		|ИЗ
		|	втТовары КАК втТовары";
    
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Склад", СкладОтправитель);
    
	Выборка = Запрос.Выполнить().Выбрать();   
	Пока Выборка.Следующий() Цикл 
		Движение = Движения.Остатки.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
		// Т.к. документ формируется на основании регистра, то вероятность получения 
		// отрицательных остатков (и последующего отката транзакции) небольшая и можно 
		// сразу сделать и приход на склад назначения.
		// Понимаю минус - наложится лишняя блокировка по складу назначения. 
		Движение = Движения.Остатки.ДобавитьПриход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Склад = СкладНазначение;
	КонецЦикла;

	Движения.Остатки.БлокироватьДляИзменения = Истина;
	Движения.Остатки.Записывать = Истина;
	Движения.Записать();

    Запрос.Текст =
		"ВЫБРАТЬ
		|	Остатки.Товар КАК Товар,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Остатки.Товар) КАК ТоварПредставление,
		|	-Остатки.КоличествоОстаток КАК Дефицит,
		|	Остатки.ГрузовоеМесто,
		|	Остатки.Товар,
		|	Остатки.КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.Остатки.Остатки(&МоментВремени, (Товар, ГрузовоеМесто) В
		|		(ВЫБРАТЬ
		|			втТовары.Товар КАК Товар,
		|			втТовары.ГрузовоеМесто КАК ГрузовоеМесто
		|		ИЗ
		|			втТовары КАК втТовары)
		|	И Склад = &Склад) КАК Остатки
		|ГДЕ
		|	Остатки.КоличествоОстаток < 0";
   
    Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени()));
	Запрос.УстановитьПараметр("Склад", СкладОтправитель);
    РезультатЗапроса = Запрос.Выполнить();
   
    Если Не РезультатЗапроса.Пустой() Тогда
        Отказ = Истина;
        ВыборкаОшибки = РезультатЗапроса.Выбрать();
        Пока ВыборкаОшибки.Следующий() Цикл
            Сообщение = Новый СообщениеПользователю;
            Сообщение.Текст = СтрШаблон(НСтр("ru='Товара <%1> в грузовом месте <%2> недостаточно в количестве <%3> шт.'"), 
											ВыборкаОшибки.ТоварПредставление, ВыборкаОшибки.ГрузовоеМесто, ВыборкаОшибки.Дефицит);
            Сообщение.Сообщить();
        КонецЦикла;
    КонецЕсли;
КонецПроцедуры
#КонецОбласти

#КонецЕсли